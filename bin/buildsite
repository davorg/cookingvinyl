#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use Template;

open my $cds_fh, '<', 'data/cds.txt' or die $!;
my $cds;

my $tt = Template->new({
  INCLUDE_PATH => 'templates',
  OUTPUT_PATH  => 'site',
});

my @cd_cols = qw[code year title];
my @track_cols = qw[num title artist];
while (<$cds_fh>) {
  chomp;
  my %cd;
  @cd{@cd_cols} = split /\t/;
  $cd{num} = (split ' ', $cd{code})[-1];
  push @$cds, \%cd;

  next unless -f "data/grillcd$cd{num}.txt";

  open my $track_fh, '<', "data/grillcd$cd{num}.txt"
    or die "$!: grillcd$cd{num}.txt";
  while (my $track = <$track_fh>) {
    chomp $track;
    my %track;
    @track{@track_cols} = split /\t/, $track;
    push @{$cd{tracks}}, \%track;
  }

  $tt->process('grillcd.tt', { cd => \%cd}, "grillcd$cd{num}.html")
    or die $tt->error;
}

$tt->process('index.tt', { cds => $cds }, 'index.html')
  or die $tt->error;
