#!/usr/bin/perl

use strict;
use warnings;
use 5.022;

use lib 'lib';
use Cooking::Schema;
use Template;

my $uri = 'https://cookingvinyl.dave.org.uk';

my $sch = Cooking::Schema->get_schema;
my $tt = Template->new({
  INCLUDE_PATH => [ qw(in tt_lib) ],
  OUTPUT_PATH  => 'docs',
});

my @albums  = $sch->resultset('Album')->all;
my @artists = $sch->resultset('Artist')->search(undef, {
  order_by => 'name',
})->all;
my @songs   = $sch->resultset('Song')->search(undef, {
  order_by => 'title',
})->all;

my %file = (
  index => 'index.html',
  about => 'about/index.html',
);
for (qw[index about]) {
  my $vars = { canonical => "$uri/" };
  $vars->{canonical} .= 'about/' if $_ eq 'about';
  $tt->process("$_.tt", $vars, $file{$_})
  or die $tt->error;
}

$tt->process('albums.tt', {
  albums => \@albums,
  title  => 'List of Albums',
  canonical => "$uri/albums/",
}, 'albums/index.html')
  or die $tt->error;

$tt->process('artists.tt', {
  artists => \@artists,
  title   => 'List of Artists',
  canonical => "$uri/artists/",
}, 'artists/index.html')
  or die $tt->error;

$tt->process('songs.tt', {
  songs => \@songs,
  title => 'List of Songs',
  canonical => "$uri/songs/",
}, 'songs/index.html')
  or die $tt->error;

foreach (@albums) {
  $tt->process('album.tt', {
    album => $_,
    tracks => [ $_->tracks->by_title ],
    title => $_->page_title,
    canonical => "$uri/" . $_->filename,
  }, $_->filename)
    or die $tt->error;
}

foreach (@artists) {
  $tt->process('artist.tt', {
    artist => $_,
    tracks => [ $_->tracks->by_title ],
    title  => $_->page_title,
    canonical => "$uri/" . $_->filename,
  }, $_->filename)
    or die $tt->error;
}

foreach (@songs) {
  $tt->process('song.tt', {
    song => $_,
    tracks => [ $_->tracks->by_title ],
    title  => $_->page_title,
    canonical => "$uri/" . $_->filename,
  }, $_->filename)
    or die $tt->error;
}
