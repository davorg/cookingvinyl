#!/usr/bin/perl

use strict;
use warnings;
use 5.022;

use lib 'lib';
use Cooking::Schema;
use Template;

my $sch = Cooking::Schema->get_schema;
my $tt = Template->new({
  INCLUDE_PATH => [ qw(in tt_lib) ],
  OUTPUT_PATH  => 'out',
});

my @albums  = $sch->resultset('Album')->all;
my @artists = $sch->resultset('Artist')->search(undef, {
  order_by => 'name',
})->all;
my @songs   = $sch->resultset('Song')->search(undef, {
  order_by => 'title',
})->all;

for (qw[index about]) {
  $tt->process("$_.tt", undef, "$_.html")
  or die $tt->error;
}

$tt->process('albums.tt', {
  albums => \@albums,
  title  => 'Albums',
}, 'albums/index.html')
  or die $tt->error;

$tt->process('artists.tt', {
  artists => \@artists,
  title   => 'Artists',
}, 'artists/index.html')
  or die $tt->error;

$tt->process('songs.tt', {
  tracks => \@songs,
  title  => 'Songs',
}, 'songs/index.html')
  or die $tt->error;

foreach (@albums) {
  $tt->process('album.tt', {
    album => $_,
    tracks => [ $_->tracks ],
    title => $_->page_title,
  }, $_->filename)
    or die $tt->error;
}

foreach (@artists) {
  $tt->process('artist.tt', {
    artist => $_,
    tracks => [ $_->tracks ],
    title  => $_->page_title,
  }, $_->filename)
    or die $tt->error;
}

foreach (@songs) {
  $tt->process('song.tt', {
    song => $_,
    tracks => [ $_->tracks ],
    title  => $_->page_title,
  }, $_->filename)
    or die $tt->error;
}
